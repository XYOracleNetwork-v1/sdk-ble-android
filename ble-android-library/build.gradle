apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

group = 'network.xyo'

buildscript {

    ext {
        experimentalsEnabled = [
                "-Xuse-experimental=kotlin.Experimental",
                "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi"
        ]
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.0.0"
    }
}

def majorVersion = 3
def minorVersion = 0

Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')
if (versionPropsFile.exists())
    versionProps.load(new FileInputStream(versionPropsFile))
def code = (versionProps['VERSION_CODE'] ?: "0").toInteger() + 1
def verString = majorVersion + '.' + minorVersion + '.' + code

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        defaultPublishConfig 'release'
        versionCode = majorVersion * 100000000 + minorVersion * 1000000 + code
        versionName majorVersion + "." + minorVersion + "." + code
    }

    defaultPublishConfig "release"

    lintOptions {
        htmlReport false
        abortOnError true
        warningsAsErrors true
        disable "HardwareIds", "MissingPermission"
    }

    sourceSets {
        main {
            assets {
                srcDir 'assets'
            }
            java {
                srcDirs += 'src/main/kotlin'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    packagingOptions {
        exclude 'META-INF/build.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/ble-android-library-release.aar")
            groupId 'network.xyo'
            artifactId 'sdk-ble-android'
            version verString

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                //def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    api 'org.jetbrains.kotlin:kotlin-reflect:1.3.61'
    api 'org.jetbrains:annotations:17.0.0'
    implementation project(':sdk-base-android')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all { task ->
    task.kotlinOptions.freeCompilerArgs += experimentalsEnabled
}