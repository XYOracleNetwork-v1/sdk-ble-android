apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'network.xyo'

buildscript {

    ext {
        experimentalsEnabled = [
                "-Xuse-experimental=kotlin.Experimental",
                "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi"
        ]
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.3.0"
    }
}

apply plugin: "org.jlleitschuh.gradle.ktlint"

Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')

if (versionPropsFile.exists())
    versionProps.load(new FileInputStream(versionPropsFile))

def increment = 0

def runTasks = gradle.startParameter.taskNames

if (':ble-android-library:assemble' in runTasks) {
    increment = 1
}

def majorVersion = 3
def minorVersion = 1
def patch = versionProps['VERSION_PATCH'].toInteger() + increment

def verString = majorVersion + '.' + minorVersion + '.' + patch

task printVersion {
    println verString
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    versionProps['VERSION_PATCH'] = patch.toString()
    versionProps.store(versionPropsFile.newWriter(), null) 

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    sourceSets {
        main {
            java {
                srcDirs += 'src/main/kotlin'
            }
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        defaultPublishConfig 'release'
        versionCode = majorVersion * 100000000 + minorVersion * 1000000 + patch
        versionName majorVersion + "." + minorVersion + "." + patch
    }

    defaultPublishConfig "release"

    lintOptions {
        htmlReport false
        abortOnError true
        warningsAsErrors true
        disable "HardwareIds", "MissingPermission"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    packagingOptions {
        exclude 'META-INF/build.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/ble-android-library-release.aar")
            groupId 'network.xyo'
            artifactId 'sdk-ble-android'
            version verString

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'xyo'
        name = 'sdk-ble-android'
        userOrg = 'xyoraclenetwork'
        licenses = ['LGPL-3.0']
        desc = 'Android Base Classes'
        vcsUrl = 'https://github.com/xyoraclenetwork/sdk-ble-android.git'

        version {
            name = verString
            vcsTag = verString
        }
    }
    publications = ['Production']
}

ktlint {
    disabledRules = ["no-wildcard-imports"]
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    api 'org.jetbrains.kotlin:kotlin-reflect:1.3.72'
    api 'network.xyo:sdk-base-android:2.1.288'
    api 'org.jetbrains:annotations:17.0.0'
    api 'org.bouncycastle:bcpkix-jdk15on:1.64'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all { task ->
    task.kotlinOptions.freeCompilerArgs += experimentalsEnabled
}